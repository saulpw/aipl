#!/usr/bin/env bin/aipl

# Cluster embeddings in database into 10 categories; summarize a random sample
# of each; query GPT for a theme and subreddit; store all in db.

# the input is some urls
!name sourceurl
!fetch-url
!extract-links baseurl={sourceurl}
!name url

# get only urls below the given url and remove the #fragment
!match ^{sourceurl}
!filter
!url-defrag>mainurl

!fetch-url
!extract-text>text

!split maxsize=4000
!take 1
!ravel

# get an embedding for each
# XXX: how to exclude for URLs we've already done?
!llm-embedding model=text-embedding-ada-002

!cluster n=10
!name label

!columns url=mainurl text=text label=label

!dbdrop categorized
!dbinsert categorized

# make a 2-column table (first being the category label, second being the list of rows)
!groupby label

# find topic and subreddit
!sample n=3

# get the text extraction from above
!ref text

# split into LLM-sized chunks
!split

# take the first chunk
!take 1

# prompt engineer that shit
!format

URL: {url}
"""
{_}
"""

!join
!format
What specific topic would these webpages fall under?
Suggest a theme of only a few words, suitable as a title for the list containing these pages.
Also suggest the subreddit that would most welcome the content.

Summaries:

{_}

Theme and subreddit:

!llm max-tokens=64 model=gpt-3.5-turbo
!print
